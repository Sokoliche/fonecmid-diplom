
Процедура ОбработкаПроведения(Отказ, Режим)
	
	
	ФормированиеДвиженийОклад();
	ФормированиеДвиженийОтпуск();
	
	Движения.ВКМ_ОсновныеНачисления.Записать(); 
	Движения.ВКМ_Удержания.Записать();
	
	РасчетОклада();
	РасчетОтпуска(); 
	
	ФормированиеДвиженийВзаиморасчетыССотрудниками();
	
КонецПроцедуры  

Процедура ФормированиеДвиженийОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад,
	|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
	|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания,
	|	ВКМ_НачислениеЗарплатыНачисления.ГрафикРаботы КАК ГрафикРаботы,
	|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот, 0) КАК ПроцентОтРабот
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, ) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВКМ_НачислениеЗарплатыНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Оклад КАК Оклад,
	|	ВТ.Сотрудник КАК Сотрудник,
	|	ВТ.ВидРасчета КАК ВидРасчета,
	|	ВТ.ДатаНачала КАК ДатаНачала,
	|	ВТ.ДатаОкончания КАК ДатаОкончания,
	|	ВТ.ГрафикРаботы КАК ГрафикРаботы,
	|	ВТ.ПроцентОтРабот КАК ПроцентОтРабот,
	|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаВыпРаб
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(, , День, ) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
	|		ПО (ВКМ_ВыполненныеСотрудникомРаботыОбороты.Период МЕЖДУ ВТ.ДатаНачала И ВТ.ДатаОкончания
	|				И ВТ.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник)";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		
		Если Выборка.СуммаВыпРаб > 0 Тогда 
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ВидРасчета = Выборка.ВидРасчета;
			Движение.ПериодДействияНачало = Выборка.ДатаНачала;
			Движение.ПериодДействияКонец = КонецДня(Выборка.ДатаОкончания);
			Движение.ПериодРегистрации = Дата;
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.Процент = Выборка.ПроцентОтРабот;			
			Движение.Оклад = Выборка.Оклад; 
			
			ДвижениеДопВып = Движения.ВКМ_ОсновныеНачисления.Добавить();
			ДвижениеДопВып.ВидРасчета = Выборка.ВидРасчета;
			ДвижениеДопВып.ПериодДействияНачало = Выборка.ДатаНачала;
			ДвижениеДопВып.ПериодДействияКонец = КонецДня(Выборка.ДатаОкончания);
			ДвижениеДопВып.ПериодРегистрации = Дата;
			ДвижениеДопВып.Сотрудник = Выборка.Сотрудник;
			ДвижениеДопВып.ВыпРаб = Выборка.СуммаВыпРаб;			
			
			
			ДвижениеНДФЛДопВып = Движения.ВКМ_Удержания.Добавить();
			ДвижениеНДФЛДопВып.ПериодРегистрации = Дата;
			ДвижениеНДФЛДопВып.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ_13;
			ДвижениеНДФЛДопВып.Сотрудник = Выборка.Сотрудник;
			ДвижениеНДФЛДопВып.ВидУдержания = Выборка.ВидРасчета;
			
			ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
			ДвижениеНДФЛ.ПериодРегистрации = Дата;
			ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ_13;
			ДвижениеНДФЛ.Сотрудник = Выборка.Сотрудник;
			ДвижениеНДФЛ.ВидУдержания = Выборка.ВидРасчета;
			
		Иначе 
			
			Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
			Движение.ВидРасчета = Выборка.ВидРасчета;
			Движение.ПериодДействияНачало = Выборка.ДатаНачала;
			Движение.ПериодДействияКонец = КонецДня(Выборка.ДатаОкончания);
			Движение.ПериодРегистрации = Дата;
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.Процент = Выборка.ПроцентОтРабот;			
			Движение.Оклад = Выборка.Оклад; 
			
			ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
			ДвижениеНДФЛ.ПериодРегистрации = Дата;
			ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ_13;
			ДвижениеНДФЛ.Сотрудник = Выборка.Сотрудник;
			ДвижениеНДФЛ.ВидУдержания = Выборка.ВидРасчета;
			
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры 

Процедура ФормированиеДвиженийОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник,
	|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета КАК ВидРасчета,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала КАК ДатаНачала,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &ВидРасчета";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		Движение.ВидРасчета = Выборка.ВидРасчета;
		Движение.ПериодДействияНачало = Выборка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Выборка.ДатаОкончания);
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Дата, - 12));
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		
		
		ДвижениеНДФЛ = Движения.ВКМ_Удержания.Добавить();
		ДвижениеНДФЛ.ПериодРегистрации = Дата;
		ДвижениеНДФЛ.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ_13;
		ДвижениеНДФЛ.Сотрудник = Выборка.Сотрудник;
		ДвижениеНДФЛ.ВидУдержания = Выборка.ВидРасчета;
		
	КонецЦикла;	
	
КонецПроцедуры 

Процедура РасчетОклада()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК Факт,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Оклад КАК Оклад,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Процент КАК Процент,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия КАК План,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВыпРаб КАК СуммаВыпРаб
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|ГДЕ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Регистратор = &Регистратор
	|	И ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета = &ВидРасчета";  
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СуммаДоВычетаНДФЛ = (Выборка.Оклад * Выборка.Факт / Выборка.План) + Выборка.СуммаВыпРаб;
		
		ДвижениеНДФЛ = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];	
		ДвижениеНДФЛ.Результат = СуммаДоВычетаНДФЛ * 0.13;
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];		
		Движение.КоличествоДней = Выборка.Факт;  	
		Движение.Результат = СуммаДоВычетаНДФЛ; 
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(,Истина);
	Движения.ВКМ_Удержания.Записать(, Истина);
	
КонецПроцедуры

Процедура РасчетОтпуска()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.БазовыйПериодКонец КАК БазовыйПериодКонец,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|			Регистратор = &Ссылка
	|				И ВидРасчета = &ВидРасчета) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблица.ВидРасчета КАК ВидРасчета,
	|	ВременнаяТаблица.ЗначениеФактическийПериодДействия КАК КоличествоДнейОтпуска,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК ОкладБаза,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.КоличествоДнейБаза, 0) КАК КоличествоРабочихДнейБаза,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК ПремияБаза,
	|	ВременнаяТаблица.Сотрудник КАК Сотрудник
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	|				&МассивИзмерений,
	|				&МассивИзмерений,
	|				,
	|				Сотрудник В
	|					(ВЫБРАТЬ
	|						ВременнаяТаблица.Сотрудник КАК Сотрудник
	|					ИЗ
	|						ВременнаяТаблица КАК ВременнаяТаблица)) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	|		ПО ВременнаяТаблица.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(
	|				&МассивИзмерений,
	|				&МассивИзмерений,
	|				,
	|				Сотрудник В
	|					(ВЫБРАТЬ
	|						ВременнаяТаблица.Сотрудник КАК Сотрудник
	|					ИЗ
	|						ВременнаяТаблица КАК ВременнаяТаблица)) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
	|		ПО ВременнаяТаблица.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки";
	
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск); 
	
	Измерение = Новый Массив;
	Измерение.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("МассивИзмерений", Измерение);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);     
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СуммаДоВычетаНДФЛ = ((Выборка.ОкладБаза + Выборка.ПремияБаза) / Выборка.КоличествоРабочихДнейБаза) 
		* Выборка.КоличествоДнейОтпуска;
		
		ДвижениеНДФЛ = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];	
		ДвижениеНДФЛ.Результат = СуммаДоВычетаНДФЛ * 0.13;
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1]; 		
		Движение.КоличествоДней = Выборка.КоличествоДнейОтпуска;
		Движение.Результат = СуммаДоВычетаНДФЛ; 
		
	КонецЦикла;	 
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина); 
	Движения.ВКМ_Удержания.Записать(, Истина);
	
КонецПроцедуры 

Процедура ФормированиеДвиженийВзаиморасчетыССотрудниками()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.Регистратор КАК Регистратор,
	|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(СУММА(ВКМ_ОсновныеНачисления.Результат), 0) КАК Результат,
	|	NULL КАК Результат1
	|ПОМЕСТИТЬ ВременнаяТаблица
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ОсновныеНачисления.Регистратор,
	|	ВКМ_ОсновныеНачисления.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВКМ_Удержания.Регистратор,
	|	ВКМ_Удержания.Сотрудник,
	|	NULL,
	|	ЕСТЬNULL(СУММА(ВКМ_Удержания.Результат), 0)
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_Удержания.Регистратор,
	|	ВКМ_Удержания.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.Регистратор КАК Регистратор,
	|	ВременнаяТаблица.Сотрудник КАК Сотрудник,
	|	СУММА(ВременнаяТаблица.Результат) КАК ОкладОтпуск,
	|	СУММА(ВременнаяТаблица.Результат1) КАК НДФЛ
	|ИЗ
	|	ВременнаяТаблица КАК ВременнаяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.Регистратор,
	|	ВременнаяТаблица.Сотрудник";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Регистратор = Выборка.Регистратор;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.ОкладОтпуск - Выборка.НДФЛ;
		
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
КонецПроцедуры
